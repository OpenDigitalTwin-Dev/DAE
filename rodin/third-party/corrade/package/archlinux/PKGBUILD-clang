# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev.clang
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (built with Clang)"
arch=('i686' 'x86_64')
url="https://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'ninja' 'clang')
options=(!strip)
provides=('corrade-git')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-clang" ] ; then
        mkdir "$_rootdir/build-clang"
        cd "$_rootdir/build-clang"

        # RelWithDebInfo is enabled but not built -- it's meant for profiling
        # from within the build dir
        cmake .. \
            -DCMAKE_CONFIGURATION_TYPES="Debug;Release;RelWithDebInfo" \
            -DCMAKE_CROSS_CONFIGS=all \
            -DCMAKE_CXX_FLAGS_RELWITHDEBINFO="-O2 -g -DNDEBUG -fno-omit-frame-pointer" \
            -DCMAKE_CXX_COMPILER=clang++ \
            -G "Ninja Multi-Config"
    fi

    cd "$_rootdir/build-clang"

    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCORRADE_BUILD_TESTS=ON
    ninja all:Debug all:Release
}

check() {
    cd "$_rootdir/build-clang"
    export CORRADE_TEST_COLOR=ON

    for config in Debug Release; do
        # The Tester test *needs* benchmarks enabled to pass properly
        ctest -C $config --output-on-failure -R TestSuiteTesterTest
        CORRADE_TEST_SKIP_BENCHMARKS=ON ctest -C $config --output-on-failure -j9 -E TestSuiteTesterTest
    done
}

package() {
    cd "$_rootdir/build-clang"
    DESTDIR="$pkgdir/" ninja install:Debug

    # Prevent the Release build from overwriting Debug binaries
    for i in $(ls $pkgdir/usr/bin/corrade-*); do
        mv ${i}{,d}
    done

    DESTDIR="$pkgdir/" ninja install/strip:Release
}
